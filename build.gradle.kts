import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jlleitschuh.gradle.ktlint.KtlintExtension
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
  id("uk.gov.justice.hmpps.gradle-spring-boot") version "11.0.9"
  id("org.openapi.generator") version "7.25.0"
  kotlin("plugin.spring") version "2.4.20"
  kotlin("plugin.jpa") version "2.4.20"
}

dependencies {
  // HMPPS dependencies
  implementation("uk.gov.justice.service.hmpps:hmpps-kotlin-spring-boot-starter:3.0.2")
  implementation("uk.gov.justice.service.hmpps:hmpps-sqs-spring-boot-starter:7.4.1")

  // Spring boot dependencies
  implementation("org.springframework.boot:spring-boot-starter-webflux")
  implementation("org.springframework.boot:spring-boot-starter-data-jpa")
  implementation("org.springframework.boot:spring-boot-starter-security")
  implementation("org.springframework.boot:spring-boot-starter-webclient")
  implementation("org.springframework.boot:spring-boot-starter-flyway")
  implementation("org.springframework.boot:spring-boot-starter-validation")

  // OpenAPI dependencies
  implementation("org.springdoc:springdoc-openapi-starter-webmvc-api:3.1.1")
  implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:3.1.1")
  implementation("org.springdoc:springdoc-openapi-starter-common:3.1.1")

  // Postgresql dependencies
  runtimeOnly("org.flywaydb:flyway-database-postgresql")
  runtimeOnly("org.postgresql:postgresql:42.7.13")

  // Open telemetry dependencies
  implementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:2.31.1")

  // Gov Notify client
  implementation("uk.gov.service.notify:notifications-java-client:6.2.1-RELEASE")

  // Test dependencies
  testImplementation("uk.gov.justice.service.hmpps:hmpps-kotlin-spring-boot-starter-test:3.0.2")
  testImplementation("org.springframework.security:spring-security-test:7.1.1")
  testImplementation("org.springframework.boot:spring-boot-starter-webmvc-test")
  testImplementation("org.springframework.boot:spring-boot-starter-data-jpa-test")
  testImplementation("org.springframework.boot:spring-boot-starter-webflux-test")
  testImplementation("org.springframework.boot:spring-boot-starter-webclient-test")

  // SAR test library
  testImplementation("uk.gov.justice.service.hmpps:hmpps-subject-access-request-test-support:2.8.1")

  testImplementation("org.awaitility:awaitility-kotlin:4.3.0")

  // JSON web token
  testImplementation("io.jsonwebtoken:jjwt-impl:0.13.0")
  testImplementation("io.jsonwebtoken:jjwt-jackson:0.13.0")

  // JUnit
  testImplementation("net.javacrumbs.json-unit:json-unit:6.2.0")
  testImplementation("net.javacrumbs.json-unit:json-unit-assertj:6.2.0")
  testImplementation("net.javacrumbs.json-unit:json-unit-json-path:5.1.2")

  // Mockito
  testImplementation("org.mockito:mockito-inline:5.2.0")

  // Test containers
  testImplementation("org.testcontainers:postgresql:1.21.4")

  // Wiremock
  testImplementation("org.wiremock:wiremock-standalone:3.13.2")

  // Swagger
  testImplementation("io.swagger.parser.v3:swagger-parser:2.1.48") {
    exclude(group = "io.swagger.core.v3")
  }
}

kotlin {
  jvmToolchain(25)
}

val openApiModelTasks = listOf(
  "buildLocationsInsidePrisonApiModel",
  "buildManageUsersApiModel",
  "buildPersonalRelationshipsApiModel",
  "buildNonAssociationsApiModel",
  "buildPrisonerSearchApiModel",
  "buildAlertsApiModel",
  "buildPrisonRegisterApiModel",
)

tasks {
  withType<KotlinCompile> {
    dependsOn(openApiModelTasks)
    compilerOptions.jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_25
    compilerOptions.freeCompilerArgs.add("-Xannotation-default-target=param-property")
  }
}

val configValues = mapOf(
  "dateLibrary" to "java8-localdatetime",
  "serializationLibrary" to "jackson",
  "enumPropertyNaming" to "original",
)

val buildDirectory: Directory = layout.buildDirectory.get()

tasks.register("buildLocationsInsidePrisonApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/locations-inside-prison-api.json")
  outputDir.set("$buildDirectory/generated/locationsinsideprisonapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.locationsinsideprison.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildManageUsersApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/manage-users-api.json")
  outputDir.set("$buildDirectory/generated/manageusersapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.manageusers.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildPersonalRelationshipsApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/personal-relationships-api.json")
  outputDir.set("$buildDirectory/generated/personalrelationships")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.personalrelationships.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildNonAssociationsApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/non-associations-api.json")
  outputDir.set("$buildDirectory/generated/nonassociationsapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.nonassociations.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildPrisonerSearchApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/prisoner-search-api.json")
  outputDir.set("$buildDirectory/generated/prisonersearchapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.prisonersearch.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildAlertsApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/alerts-api.json")
  outputDir.set("$buildDirectory/generated/alertsapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.alertsapi.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

tasks.register("buildPrisonRegisterApiModel", GenerateTask::class) {
  generatorName.set("kotlin")
  inputSpec.set("openapi-specs/prison-register-api.json")
  outputDir.set("$buildDirectory/generated/prisonregisterapi")
  modelPackage.set("uk.gov.justice.digital.hmpps.officialvisitsapi.client.prisonregisterapi.model")
  configOptions.set(configValues)
  globalProperties.set(mapOf("models" to ""))
}

val generatedProjectDirs = listOf("locationsinsideprisonapi", "manageusersapi", "personalrelationships", "nonassociationsapi", "prisonersearchapi", "alertsapi", "prisonregisterapi")

tasks.register("integrationTest", Test::class) {
  description = "Runs integration tests"
  group = "verification"
  testClassesDirs = sourceSets["test"].output.classesDirs
  classpath = sourceSets["test"].runtimeClasspath

  useJUnitPlatform {
    filter {
      includeTestsMatching("*.integration.*")
    }
  }

  shouldRunAfter("test")
  maxHeapSize = "2048m"
}
tasks.named<Test>("test") {
  filter {
    excludeTestsMatching("*.integration.*")
  }
}

kotlin {
  generatedProjectDirs.forEach { generatedProject ->
    sourceSets["main"].apply {
      kotlin.srcDir("$buildDirectory/generated/$generatedProject/src/main/kotlin")
    }
  }
}

tasks.named("runKtlintCheckOverMainSourceSet") {
  dependsOn(openApiModelTasks)
}

tasks.named("runKtlintFormatOverMainSourceSet") {
  dependsOn(openApiModelTasks)
}

configure<KtlintExtension> {
  filter {
    generatedProjectDirs.forEach { generatedProject ->
      exclude { element ->
        element.file.path.contains("build/generated/$generatedProject/src/main/")
      }
    }
  }
}
